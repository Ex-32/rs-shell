
WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "#" ~ ( !"\n" ~ ANY )* }

Line = { SOI ~ Expression  ~ EOI }

Expression = {
    ( VarDef* ~ Text+ ~ (Operator ~ Expression?)? ) |
    ( VarDef* ~ Text+ )
}

Operator = @{ ";" | "&&" | "&" | "||" | "|" | ">>" | ">&" | ">" | "<<<" | "<<" | "<" }

VarDef = ${ VarID ~ "=" ~ Text }
VarID = @{ ( ASCII_ALPHANUMERIC | "_" )+ }

Text = { Expansion | RawText | SingleString | DoubleString }

    RawText = @{ ( !( WHITESPACE | Operator | "=" ) ~ ANY )+ }
    SingleString = ${ "'" ~  (!"'" ~ ANY  )*  ~ "'"  }
    DoubleString = ${ "\"" ~ ( !"\"" ~ ( Expansion | ANY ) )* ~ "\"" }

    Expansion = { TextVar | CmdVar | PlainVar | CmdQuote }

        TextVar = !{ "${" ~ VarID ~ "}" }
        PlainVar = ${ "$" ~ VarID }

        CmdVar = !{ "$(" ~ Expression ~ ")" }
        CmdQuote = !{ "`" ~ Expression ~ "`" }
